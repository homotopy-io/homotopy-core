#
#  eric.catt - some examples 
#

coh id (x : *) : x -> x
coh comp (x : *) (y : *) (f : x -> y) (z : *) (g : y -> z) : x -> z

coh h2 (x0 : *)
       (x1 : *) (f0 : x0 -> x1)
                (f1 : x0 -> x1) (a : f0 -> f1)
       (x2 : *) (g0 : x1 -> x2)
                (g1 : x1 -> x2) (b : g0 -> g1) : comp f0 g0 -> comp f1 g1

coh v2 (x0 : *)
       (x1 : *) (f0 : x0 -> x1)
                (f1 : x0 -> x1) (a : f0 -> f1)
                (f2 : x0 -> x1) (b : f1 -> f2) : f0 -> f2

coh v3-0 (x0 : *)
         (x1 : *) (f0 : x0 -> x1)
                  (f1 : x0 -> x1) (a0 : f0 -> f1)
                                  (a1 : f0 -> f1) (u : a0 -> a1)
         (x2 : *) (g0 : x1 -> x2)
                  (g1 : x1 -> x2) (b0 : g0 -> g1)
                                  (b1 : g0 -> g1) (v : b0 -> b1) : h2 a0 b0 -> h2 a1 b1

coh v3-1 (x0 : *)
         (x1 : *) (f0 : x0 -> x1)
                  (f1 : x0 -> x1) (a0 : f0 -> f1)
                                  (a1 : f0 -> f1) (u : a0 -> a1)
                  (f2 : x0 -> x1) (b0 : f1 -> f2)
                                  (b1 : f1 -> f2) (v : b0 -> b1) : v2 a0 b0 -> v2 a1 b1

coh v3-2 (x0 : *)
         (x1 : *) (f0 : x0 -> x1)
                  (f1 : x0 -> x1) (a0 : f0 -> f1)
                                  (a1 : f0 -> f1) (u : a0 -> a1)
                                  (a2 : f0 -> f1) (v : a1 -> a2) : a0 -> a2

coh ichg (x0 : *)
         (x1 : *) (f0 : x0 -> x1)
                  (f1 : x0 -> x1) (a : f0 -> f1)
                  (f2 : x0 -> x1) (b : f1 -> f2)
         (x2 : *) (g0 : x1 -> x2)
                  (g1 : x1 -> x2) (c : g0 -> g1)
                  (g2 : x1 -> x2) (d : g1 -> g2) : h2 (v2 a b) (v2 c d) -> v2 (h2 a c) (h2 b d)

coh twobytwo (x0 : *)
             (x1 : *) (f0 : x0 -> x1)
                      (f1 : x0 -> x1) (a : f0 -> f1)
                      (f2 : x0 -> x1) (b : f1 -> f2)
             (x2 : *) (g0 : x1 -> x2)
                      (g1 : x1 -> x2) (c : g0 -> g1)
                      (g2 : x1 -> x2) (d : g1 -> g2) : comp f0 g0 -> comp f2 g2

coh twobytwo3d (x0 : *)
               (x1 : *) (f0 : x0 -> x1)
                        (f1 : x0 -> x1) (a0 : f0 -> f1)
                                        (a1 : f0 -> f1) (s : a0 -> a1)
                        (f2 : x0 -> x1) (b0 : f1 -> f2)
                                        (b1 : f1 -> f2) (t : b0 -> b1)
               (x2 : *) (g0 : x1 -> x2)
                        (g1 : x1 -> x2) (c0 : g0 -> g1)
                                        (c1 : g0 -> g1) (u : c0 -> c1)
                        (g2 : x1 -> x2) (d0 : g1 -> g2)
                                        (d1 : g1 -> g2) (v : d0 -> d1) : twobytwo a0 b0 c0 d0 -> twobytwo a1 b1 c1 d1

let interp2  (x0 : *)
             (x1 : *) (f0 : x0 -> x1)
                      (f1 : x0 -> x1) (a : f0 -> f1)
                      (f2 : x0 -> x1) (b : f1 -> f2)
             (x2 : *) (g0 : x1 -> x2)
                      (g1 : x1 -> x2) (c : g0 -> g1)
                      (g2 : x1 -> x2) (d : g1 -> g2) = v2 (h2 a c) (h2 b d)

let three2x2x1 (x0 : *)
               (x1 : *) (f0 : x0 -> x1)
                        (f1 : x0 -> x1) (a0 : f0 -> f1)
                                        (a1 : f0 -> f1) (s : a0 -> a1)
                        (f2 : x0 -> x1) (b0 : f1 -> f2)
                                        (b1 : f1 -> f2) (t : b0 -> b1)
               (x2 : *) (g0 : x1 -> x2)
                        (g1 : x1 -> x2) (c0 : g0 -> g1)
                                        (c1 : g0 -> g1) (u : c0 -> c1)
                        (g2 : x1 -> x2) (d0 : g1 -> g2)
                                        (d1 : g1 -> g2) (v : d0 -> d1) = v3-1 (v3-0 s u) (v3-0 t v)

let cube3 (x0 : *)
          (x1 : *) (f0 : x0 -> x1)
                   (f1 : x0 -> x1) (a0 : f0 -> f1)
                                   (a1 : f0 -> f1) (s0 : a0 -> a1)
                                   (a2 : f0 -> f1) (s1 : a1 -> a2)
                   (f2 : x0 -> x1) (b0 : f1 -> f2)
                                   (b1 : f1 -> f2) (t0 : b0 -> b1)
                                   (b2 : f1 -> f2) (t1 : b1 -> b2)
          (x2 : *) (g0 : x1 -> x2)
                   (g1 : x1 -> x2) (c0 : g0 -> g1)
                                   (c1 : g0 -> g1) (u0 : c0 -> c1)
                                   (c2 : g0 -> g1) (u1 : c1 -> c2)
                   (g2 : x1 -> x2) (d0 : g1 -> g2)
                                   (d1 : g1 -> g2) (v0 : d0 -> d1)
                                   (d2 : g1 -> g2) (v1 : d1 -> d2) = v3-2 (three2x2x1 s0 t0 u0 v0) (three2x2x1 s1 t1 u1 v1)
                                   